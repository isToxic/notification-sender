import org.yaml.snakeyaml.Yaml

buildscript {
	dependencies {
		classpath group: 'org.yaml', name: 'snakeyaml', version: '1.27'
	}
}

plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.flywaydb.flyway" version "7.5.3"
	id 'nu.studer.jooq' version '4.1'
	id 'java'
}

apply from: 'gradle/dependencies.gradle'

group = 'org.zgr'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.inputs.files(processResources)

def adr = System.getenv('db_address') ?: '10.241.0.127'
def port = System.getenv('db_port') ?: '5432'
def dbn = System.getenv('db_name') ?: 'notification-service'
def dbUsername = System.getenv('db_user') ?: 'postgres'
def dbPassword = System.getenv('db_pass') ?: 'test'
def dbUrl = "jdbc:postgresql://" + adr + ":" + port + "/" + dbn

jooq() {
	version = '3.14.7'
	edition = 'OSS'

	notificationSender(sourceSets.main) {
		jdbc {
			url = dbUrl
			user = dbUsername
			password = dbPassword
			driver = 'org.postgresql.Driver'
		}
		generator {
			name = 'org.jooq.codegen.JavaGenerator'
			generate {
				validationAnnotations = true
				daos = true
				fluentSetters = true
			}
			database {
				name = 'org.jooq.meta.postgres.PostgresDatabase'
				inputSchema = 'notification_sender'
				includes = '.*'
				excludes = 'flyway_schema_history'
			}
			target {
				packageName = 'org.zgr.notification.sender.db.jooq'
				directory = 'src/main/java'
			}
		}
	}
}

flyway {
	url = dbUrl
	user = dbUsername
	password = dbPassword
}

generateNotificationSenderJooqSchemaSource.dependsOn flywayMigrate

test {
	useJUnitPlatform()
	testLogging {
		displayGranularity = 1
		events = ["STARTED", "PASSED", "FAILED", "SKIPPED"]
	}
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor libs.lombok
	testAnnotationProcessor libs.lombok

	jooqRuntime libs.db

	compile libs.spring
	compile libs.db
	compile libs.common

	testCompile libs.test
}

configurations {
	flywayRuntime{
		extendsFrom implementation
	}
	jooqRuntime {
		extendsFrom implementation
	}
}
